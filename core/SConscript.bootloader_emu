# pylint: disable=E0602

import os
import tools
import boards

TREZOR_MODEL = ARGUMENTS.get('TREZOR_MODEL', 'T')
CMAKELISTS = int(ARGUMENTS.get('CMAKELISTS', 0))
NEW_RENDERING = ARGUMENTS.get('NEW_RENDERING', '1') == '1'
DMA2D = False

if TREZOR_MODEL in ('1', 'DISC1'):
    # skip bootloader build
    env = Environment()
    def build_bootloader(target,source,env):
        print(f'Bootloader: nothing to build for Model {TREZOR_MODEL}')
    program_bin = env.Command(
                 target='bootloader.elf',
                 source=None,
                 action=build_bootloader
             )
    Return()

FEATURES_WANTED = ["input", "rgb_led", "dma2d"]

if NEW_RENDERING:
    FEATURES_WANTED.append("new_rendering")

CCFLAGS_MOD = ''
CPPPATH_MOD = []
CPPDEFINES_MOD = []
SOURCE_MOD = []

if TREZOR_MODEL in ('1', 'R'):
    FONT_NORMAL='Font_PixelOperator_Regular_8'
    FONT_DEMIBOLD='Font_PixelOperator_Regular_8'
    FONT_BOLD='Font_PixelOperator_Bold_8'
    FONT_MONO='Font_PixelOperator_Regular_8'
    FONT_BIG=None
elif TREZOR_MODEL in ('T', 'DISC2'):
    FONT_NORMAL='Font_TTHoves_Regular_21'
    FONT_DEMIBOLD=None
    FONT_BOLD='Font_TTHoves_Bold_17'
    FONT_MONO=None
    FONT_BIG=None
elif TREZOR_MODEL in ('T3T1',):
    FONT_NORMAL='Font_TTSatoshi_DemiBold_21'
    FONT_DEMIBOLD='Font_TTSatoshi_DemiBold_21'
    FONT_BOLD='Font_TTHoves_Bold_17'
    FONT_MONO='Font_TTSatoshi_DemiBold_21'
    FONT_BIG=None

# modtrezorcrypto
CCFLAGS_MOD += '-Wno-sequence-point '
CPPPATH_MOD += [
    'vendor/trezor-crypto',
    'vendor/trezor-storage',
]
CPPDEFINES_MOD += [
    'AES_128',
    'AES_192',
    'USE_KECCAK',
    'ED25519_NO_PRECOMP',
    'TREZOR_UI2',
    'FANCY_FATAL_ERROR'
]
SOURCE_MOD += [
    'vendor/trezor-crypto/blake2s.c',
    'vendor/trezor-crypto/chacha_drbg.c',
    'vendor/trezor-crypto/chacha20poly1305/chacha_merged.c',
    'vendor/trezor-crypto/ed25519-donna/curve25519-donna-32bit.c',
    'vendor/trezor-crypto/ed25519-donna/curve25519-donna-helpers.c',
    'vendor/trezor-crypto/ed25519-donna/curve25519-donna-scalarmult-base.c',
    'vendor/trezor-crypto/ed25519-donna/ed25519.c',
    'vendor/trezor-crypto/ed25519-donna/ed25519-donna-32bit-tables.c',
    'vendor/trezor-crypto/ed25519-donna/ed25519-donna-basepoint-table.c',
    'vendor/trezor-crypto/ed25519-donna/ed25519-donna-impl-base.c',
    'vendor/trezor-crypto/ed25519-donna/modm-donna-32bit.c',
    'vendor/trezor-crypto/memzero.c',
    'vendor/trezor-crypto/rand.c',
    'vendor/trezor-crypto/sha2.c',
]

# modtrezorui
CPPPATH_MOD += [
        'vendor/micropython/lib/uzlib',
]

SOURCE_MOD += [
    'embed/extmod/modtrezorcrypto/rand.c',
    'embed/lib/buffers.c',
    'embed/lib/colors.c',
    'embed/lib/display_utils.c',
    'embed/lib/dma2d_emul.c',
    'embed/lib/fonts/font_bitmap.c',
    'embed/lib/fonts/fonts.c',
    'embed/lib/gl_color.c',
    'embed/lib/gl_bitblt_mono8.c',
    'embed/lib/gl_bitblt_rgb565.c',
    'embed/lib/gl_bitblt_rgba8888.c',
    'embed/lib/image.c',
    'embed/lib/terminal.c',
    'embed/lib/touch.c',
    'embed/lib/unit_variant.c',
    'vendor/micropython/lib/uzlib/adler32.c',
    'vendor/micropython/lib/uzlib/crc32.c',
    'vendor/micropython/lib/uzlib/tinflate.c',
    'vendor/trezor-storage/flash_area.c',
]

if NEW_RENDERING:
    CPPDEFINES_MOD += ['NEW_RENDERING']
    if TREZOR_MODEL in ('T',):
        CPPDEFINES_MOD += ['DISPLAY_RGB565']
    elif TREZOR_MODEL in ('R', '1',):
        CPPDEFINES_MOD += ['XFRAMEBUFFER', 'DISPLAY_MONO']
    elif TREZOR_MODEL in ('T3T1',):
        CPPDEFINES_MOD += ['XFRAMEBUFFER', 'DISPLAY_RGB565']
    SOURCE_MOD += [
        'embed/lib/gl_draw.c',
    ]
else:
    SOURCE_MOD += [
        'embed/lib/display_draw.c',
]

if TREZOR_MODEL in ('1', ):
    SOURCE_MOD += [
        'embed/models/model_T1B1_layout.c',
    ]
elif TREZOR_MODEL in ('T', ):
    SOURCE_MOD += [
        'embed/models/model_T2T1_layout.c',
    ]
elif TREZOR_MODEL in ('R', ):
    SOURCE_MOD += [
        'embed/models/model_T2B1_layout.c',
    ]
elif TREZOR_MODEL in ('T3T1',):
    SOURCE_MOD += [
        'embed/models/model_T3T1_layout.c',
    ]


SOURCE_NANOPB = [
    'vendor/nanopb/pb_common.c',
    'vendor/nanopb/pb_decode.c',
    'vendor/nanopb/pb_encode.c',
]

SOURCE_BOOTLOADER = [
    'embed/bootloader/bootui.c',
    'embed/bootloader/main.c',
    'embed/bootloader/messages.c',
    'embed/bootloader/emulator.c',
    'embed/bootloader/protob/messages.pb.c',
]

SOURCE_TREZORHAL = [
    'embed/trezorhal/unix/boot_args.c',
    'embed/trezorhal/unix/fault_handlers.c',
    'embed/trezorhal/unix/flash.c',
    'embed/trezorhal/unix/flash_otp.c',
    'embed/trezorhal/unix/common.c',
    'embed/trezorhal/unix/touch/touch.c',
    'embed/trezorhal/unix/rng.c',
    'embed/trezorhal/unix/usb.c',
    'embed/trezorhal/unix/random_delays.c',
    'embed/trezorhal/unix/secret.c',
]

if NEW_RENDERING:
    SOURCE_TREZORHAL += [
        'embed/trezorhal/unix/display_driver.c',
        'embed/trezorhal/unix/dma2d_bitblt.c',
        'embed/trezorhal/xdisplay_legacy.c',
    ]
else:
    SOURCE_TREZORHAL += [
        'embed/trezorhal/unix/display-unix.c',
    ]

if TREZOR_MODEL in ('R', 'T3T1'):
    SOURCE_TREZORHAL += [
        'embed/trezorhal/unix/optiga_hal.c',
    ]

SOURCE_UNIX = [
    'embed/unix/profile.c',
]



# fonts
tools.add_font('NORMAL', FONT_NORMAL, CPPDEFINES_MOD, SOURCE_MOD)
tools.add_font('BOLD', FONT_BOLD, CPPDEFINES_MOD, SOURCE_MOD)
tools.add_font('DEMIBOLD', FONT_DEMIBOLD, CPPDEFINES_MOD, SOURCE_MOD)
tools.add_font('MONO', FONT_MONO, CPPDEFINES_MOD, SOURCE_MOD)
tools.add_font('BIG', FONT_BIG, CPPDEFINES_MOD, SOURCE_MOD)

env = Environment(ENV=os.environ, CFLAGS='%s -DCONFIDENTIAL= -DPRODUCTION=%s' % (ARGUMENTS.get('CFLAGS', ''), ARGUMENTS.get('PRODUCTION', '0')))

env.Replace(
    CP='cp',
    AS='as',
    AR='ar',
    CC='gcc',
    LINK='ld',
    SIZE='size',
    STRIP='strip',
    OBJCOPY='objcopy',
    PYTHON='python',
    MAKECMAKELISTS='$PYTHON tools/make_cmakelists.py', )

env.Replace(
    TREZOR_MODEL=TREZOR_MODEL, )

if TREZOR_MODEL in ('T', 'R'):
    CPU_MODEL   = 'STM32F427xx'
elif TREZOR_MODEL in ('T3T1', ):
    CPU_MODEL   = 'STM32U585xx'
elif TREZOR_MODEL in ('DISC1', ):
    CPU_MODEL   = 'STM32F429xx'
elif TREZOR_MODEL in ('DISC2', ):
    CPU_MODEL   = 'STM32U5A9xx'
elif TREZOR_MODEL in ('1',):
    CPU_MODEL   = 'STM32F405xx'
else:
    raise ValueError('Unknown Trezor model')

MODEL_IDENTIFIER = tools.get_model_identifier(TREZOR_MODEL)
MODEL_AS_NUMBER = str(boards.get_hw_model_as_number(MODEL_IDENTIFIER))

env.Replace(
    COPT=env.get('ENV').get('OPTIMIZE', '-Os'),
    CCFLAGS='$COPT '
    '-g3 '
    '-nostdlib '
    '-std=gnu11 -Wall -Werror -Wpointer-arith -Wno-missing-braces -fno-common '
    '-fsingle-precision-constant -fdata-sections -ffunction-sections '
    '-ffreestanding '
    '-fstack-protector-all '
    + CCFLAGS_MOD,
    CPPPATH=[
        'embed/rust',
        'embed/bootloader',
        'embed/bootloader/nanopb',
        'embed/bootloader/protob',
        'embed/lib',
        'embed/models',
        'embed/trezorhal',
        'embed/trezorhal/unix',
        'embed/unix',
        'embed/extmod/modtrezorui',
        'vendor/nanopb',
    ] + CPPPATH_MOD,
    CPPDEFINES=[
        'BOOTLOADER',
        'TREZOR_EMULATOR',
        CPU_MODEL,
        'HW_MODEL=' + MODEL_AS_NUMBER,
        'HW_REVISION=' + ('10' if TREZOR_MODEL in ('R',) else '0'),
        'TREZOR_MODEL_'+TREZOR_MODEL,
        'TREZOR_BOARD=\\"boards/board-unix.h\\"',
        ('FLASH_BIT_ACCESS', '1'),
        ('FLASH_BLOCK_WORDS', '1'),
        'MCU_TYPE='+CPU_MODEL,
        'PB_FIELD_16BIT',
        'PB_ENCODE_ARRAYS_UNPACKED',
        'PB_VALIDATE_UTF8',
    ] + CPPDEFINES_MOD,
    ASPPFLAGS='$CFLAGS $CCFLAGS', )

try:
    env.ParseConfig('pkg-config --cflags --libs sdl2 SDL2_image')
except OSError:
    print("SDL2 not installed, Emulator build is not possible")


env.Replace(
    ALLSOURCES=SOURCE_MOD + SOURCE_BOOTLOADER + SOURCE_NANOPB + SOURCE_TREZORHAL + SOURCE_UNIX,
    ALLDEFS=tools.get_defs_for_cmake(env['CPPDEFINES']))

cmake_gen = env.Command(
    target='CMakeLists.txt',
    source='',
    action='$MAKECMAKELISTS --sources $ALLSOURCES --dirs $CPPPATH --defs $ALLDEFS',
)


#
# Rust library
#

RUST_TARGET = 'x86_64-unknown-linux-gnu'
RUST_LIB = 'trezor_lib'

if ARGUMENTS.get('TREZOR_EMULATOR_DEBUGGABLE', '0') == '1':
    RUST_PROFILE = 'dev'
    RUST_LIBDIR = f'build/bootloader_emu/rust/{RUST_TARGET}/debug'
else:
    RUST_PROFILE = 'release'
    RUST_LIBDIR = f'build/bootloader_emu/rust/{RUST_TARGET}/release'

RUST_LIBPATH = f'{RUST_LIBDIR}/lib{RUST_LIB}.a'

def cargo_build():
    if TREZOR_MODEL in ("1",):
        features = ["model_t1"]
    elif TREZOR_MODEL in ("R",):
        features = ["model_tr"]
    elif TREZOR_MODEL in ("T3T1",):
        features = ["model_mercury"]
    else:
        features = ["model_tt"]

    if NEW_RENDERING:
        features.append('new_rendering')
        if TREZOR_MODEL in ('T',):
            features.append('display_rgb565')
            features.append('ui_antialiasing')
        elif TREZOR_MODEL in ('R', '1',):
            features.append('display_mono')
            features.append('xframebuffer')
        elif TREZOR_MODEL in ('T3T1',):
            features.append('display_rgb565')
            features.append('xframebuffer')
            features.append('ui_antialiasing')

    if TREZOR_MODEL in ('T', 'T3T1'):
        features.append('touch')
        features.append('backlight')
        features.append('dma2d')
    if TREZOR_MODEL in ('R', '1'):
        features.append('button')

    features.append("ui")
    features.append("bootloader")

    cargo_opts = [
        f'--target={RUST_TARGET}',
        '--target-dir=../../build/bootloader_emu/rust',
        '--no-default-features',
        '--features ' + ','.join(features),
        '-Z build-std=core',
        '-Z build-std-features=panic_immediate_abort',
    ]

    return f'cd embed/rust; cargo build --profile {RUST_PROFILE} ' + ' '.join(cargo_opts)

rust = env.Command(
    target=RUST_LIBPATH,
    source='',
    action=cargo_build(), )

env.Append(LINKFLAGS=f'-L{RUST_LIBDIR}')
env.Append(LINKFLAGS=f'-l{RUST_LIB}')
env.Append(LINKFLAGS='-lm')

#
# Program objects
#

obj_program = []
obj_program += env.Object(source=SOURCE_MOD)
obj_program += env.Object(source=SOURCE_BOOTLOADER)
obj_program += env.Object(source=SOURCE_NANOPB)
obj_program += env.Object(source=SOURCE_TREZORHAL)
obj_program += env.Object(source=SOURCE_UNIX)

program_elf = env.Command(
    target='bootloader.elf',
    source=obj_program,
    action=
    '$CC -o $TARGET $SOURCES $_LIBDIRFLAGS $_LIBFLAGS $LINKFLAGS', )

env.Depends(program_elf, rust)

if CMAKELISTS != 0:
    env.Depends(program_elf, cmake_gen)
