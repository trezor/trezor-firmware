# pylint: disable=E0602

import os
import shlex
import tools, models, ui

TREZOR_MODEL = ARGUMENTS.get('TREZOR_MODEL', 'T2T1')
CMAKELISTS = int(ARGUMENTS.get('CMAKELISTS', 0))
HW_REVISION = 'emulator'

if not models.has_emulator(TREZOR_MODEL):
    # skip bootloader build
    env = Environment()
    def build_bootloader(target,source,env):
        print(f'Bootloader: nothing to build for Model {TREZOR_MODEL}')
    program_bin = env.Command(
                 target='bootloader.elf',
                 source=None,
                 action=build_bootloader
             )
    Return()

FEATURES_WANTED = ["input", "rgb_led", "dma2d", "optiga"]

CCFLAGS_MOD = ''
CPPPATH_MOD = []
CPPDEFINES_HAL = []
PATH_HAL = []
CPPDEFINES_MOD = []
SOURCE_MOD = []
SOURCE_MOD_CRYPTO = []
RUST_UI_FEATURES = []

# modtrezorcrypto
CCFLAGS_MOD += '-Wno-sequence-point '
CPPPATH_MOD += [
    'vendor/trezor-crypto',
    'vendor/trezor-storage',
]
CPPDEFINES_MOD += [
    'KERNEL_MODE',
    'AES_128',
    'AES_192',
    'USE_KECCAK',
    'ED25519_NO_PRECOMP',
    'FANCY_FATAL_ERROR'
]
SOURCE_MOD_CRYPTO += [
    'vendor/trezor-crypto/blake2s.c',
    'vendor/trezor-crypto/chacha_drbg.c',
    'vendor/trezor-crypto/chacha20poly1305/chacha_merged.c',
    'vendor/trezor-crypto/ed25519-donna/curve25519-donna-32bit.c',
    'vendor/trezor-crypto/ed25519-donna/curve25519-donna-helpers.c',
    'vendor/trezor-crypto/ed25519-donna/curve25519-donna-scalarmult-base.c',
    'vendor/trezor-crypto/ed25519-donna/ed25519.c',
    'vendor/trezor-crypto/ed25519-donna/ed25519-donna-32bit-tables.c',
    'vendor/trezor-crypto/ed25519-donna/ed25519-donna-basepoint-table.c',
    'vendor/trezor-crypto/ed25519-donna/ed25519-donna-impl-base.c',
    'vendor/trezor-crypto/ed25519-donna/modm-donna-32bit.c',
    'vendor/trezor-crypto/memzero.c',
    'vendor/trezor-crypto/rand.c',
    'vendor/trezor-crypto/sha2.c',
]

# modtrezorui
CPPPATH_MOD += [
        'vendor/micropython/lib/uzlib',
]

SOURCE_MOD += [
    'embed/upymod/modtrezorcrypto/rand.c',
    'embed/gfx/bitblt/gfx_bitblt.c',
    'embed/gfx/bitblt/gfx_bitblt_mono8.c',
    'embed/gfx/bitblt/gfx_bitblt_rgb565.c',
    'embed/gfx/bitblt/gfx_bitblt_rgba8888.c',
    'embed/gfx/fonts/font_bitmap.c',
    'embed/gfx/fonts/fonts.c',
    'embed/gfx/gfx_color.c',
    'embed/gfx/gfx_draw.c',
    'embed/gfx/terminal.c',
    'embed/io/display/display_utils.c',
    'embed/util/flash/flash_utils.c',
    'embed/util/image/image.c',
    'embed/util/rsod/rsod.c',
    'embed/util/scm_revision/scm_revision.c',
    'embed/rtl/error_handling.c',
    'embed/rtl/mini_printf.c',
    'vendor/micropython/lib/uzlib/adler32.c',
    'vendor/micropython/lib/uzlib/crc32.c',
    'vendor/micropython/lib/uzlib/tinflate.c',
    'vendor/trezor-storage/flash_area.c',
]

SOURCE_NANOPB = [
    'vendor/nanopb/pb_common.c',
    'vendor/nanopb/pb_decode.c',
    'vendor/nanopb/pb_encode.c',
]

SOURCE_BOOTLOADER = [
    'embed/projects/bootloader/bootui.c',
    'embed/projects/bootloader/main.c',
    'embed/projects/bootloader/messages.c',
    'embed/projects/bootloader/emulator.c',
    'embed/projects/bootloader/version_check.c',
    'embed/projects/bootloader/protob/messages.pb.c',
]

SOURCE_TREZORHAL = [
    'embed/io/display/unix/display_driver.c',
    'embed/io/usb/unix/usb.c',
    'embed/sec/monoctr/unix/monoctr.c',
    'embed/sec/random_delays/unix/random_delays.c',
    'embed/sec/rng/unix/rng.c',
    'embed/sec/secret/unix/secret.c',
    'embed/sys/mpu/unix/mpu.c',
    'embed/sys/startup/unix/bootutils.c',
    'embed/sys/task/unix/system.c',
    'embed/sys/time/unix/systick.c',
    'embed/sys/time/unix/systimer.c',
    'embed/util/flash/unix/flash.c',
    'embed/util/flash/unix/flash_otp.c',
    'embed/util/unit_properties/unix/unit_properties.c',
]

SOURCE_UNIX = [
    'embed/projects/unix/profile.c',
]

ui.init_ui(TREZOR_MODEL, "bootloader", CPPDEFINES_MOD, SOURCE_MOD, RUST_UI_FEATURES)

env = Environment(
    ENV=os.environ,
    CFLAGS=ARGUMENTS.get('CFLAGS', '') + f" -DCONFIDENTIAL= -DPRODUCTION={ARGUMENTS.get('PRODUCTION', '0')}",
    CPPDEFPREFIX="-D'",
    CPPDEFSUFFIX="'",
)

FEATURES_AVAILABLE = models.configure_board(TREZOR_MODEL, HW_REVISION, FEATURES_WANTED, env, CPPDEFINES_HAL, SOURCE_UNIX, PATH_HAL)

env.Replace(
    CP='cp',
    AS='as',
    AR='ar',
    CC='gcc',
    LINK='ld',

    SIZE='size',
    STRIP='strip',
    OBJCOPY='objcopy',
    PYTHON='python',
    MAKECMAKELISTS='$PYTHON tools/make_cmakelists.py', )

MODEL_AS_NUMBER = str(models.get_hw_model_as_number(TREZOR_MODEL))

ALLPATHS = ['embed/rust',
           'embed/projects/bootloader',
           'embed/projects/bootloader/nanopb',
           'embed/projects/bootloader/protob',
           'embed/rtl/inc',
           'embed/models',
           'embed/projects/unix',
           'embed/upymod/modtrezorui',
           'embed/gfx/inc',
           'embed/io/display/inc',
           'embed/io/usb/inc',
           'embed/sec/entropy/inc',
           'embed/sec/monoctr/inc',
           'embed/sec/random_delays/inc',
           'embed/sec/rng/inc',
           'embed/sec/secret/inc',
           'embed/sys/bsp/inc',
           'embed/sys/irq/inc',
           'embed/sys/mpu/inc',
           'embed/sys/startup/inc',
           'embed/sys/task/inc',
           'embed/sys/time/inc',
           'embed/util/flash/inc',
           'embed/util/image/inc',
           'embed/util/rsod/inc',
           'embed/util/scm_revision/inc',
           'embed/util/translations/inc',
           'embed/util/unit_properties/inc',
           'vendor/nanopb',
       ] + CPPPATH_MOD + PATH_HAL

env.Replace(
    COPT=env.get('ENV').get('OPTIMIZE', '-Os'),
    CCFLAGS='$COPT '
    '-g3 '
    '-nostdlib '
    '-std=gnu11 -Wall -Werror -Wpointer-arith -Wno-missing-braces -fno-common '
    '-fdata-sections -ffunction-sections '
    '-ffreestanding '
    '-fstack-protector-all '
    + CCFLAGS_MOD,
    CPPPATH=ALLPATHS,
    CPPDEFINES=[
        'BOOTLOADER',
        'TREZOR_EMULATOR',
        'TREZOR_MODEL_'+TREZOR_MODEL,
        'PB_FIELD_16BIT',
        'PB_ENCODE_ARRAYS_UNPACKED',
        'PB_VALIDATE_UTF8',
    ] + CPPDEFINES_MOD + CPPDEFINES_HAL,
    ASPPFLAGS='$CFLAGS $CCFLAGS', )

try:
    env.ParseConfig('pkg-config --cflags --libs sdl2 SDL2_image')
except OSError:
    print("SDL2 not installed, Emulator build is not possible")


env.Replace(
    ALLSOURCES=SOURCE_MOD + SOURCE_MOD_CRYPTO + SOURCE_BOOTLOADER + SOURCE_NANOPB + SOURCE_TREZORHAL + SOURCE_UNIX,
    ALLDEFS=tools.get_defs_for_cmake(env['CPPDEFINES']))

cmake_gen = env.Command(
    target='CMakeLists.txt',
    source='',
    action='$MAKECMAKELISTS --sources $ALLSOURCES --dirs $CPPPATH --defs $ALLDEFS',
)


#
# Rust library
#

RUST_TARGET = os.popen("rustc -vV | sed -n 's/host: //p'").read().strip()
RUST_LIB = 'trezor_lib'

if ARGUMENTS.get('TREZOR_EMULATOR_DEBUGGABLE', '0') == '1':
    RUST_PROFILE = 'dev'
    RUST_LIBDIR = f'build/bootloader_emu/rust/{RUST_TARGET}/debug'
else:
    RUST_PROFILE = 'release'
    RUST_LIBDIR = f'build/bootloader_emu/rust/{RUST_TARGET}/release'

RUST_LIBPATH = f'{RUST_LIBDIR}/lib{RUST_LIB}.a'

def cargo_build():
    features = []

    features.extend(RUST_UI_FEATURES)
    features.append("ui")
    features.append("bootloader")
    features.extend(FEATURES_AVAILABLE)


    cargo_opts = [
        f'--target={RUST_TARGET}',
        '--target-dir=../../build/bootloader_emu/rust',
        '--no-default-features',
        '--features ' + ','.join(features),
        '-Z build-std=core',
        '-Z build-std-features=panic_immediate_abort',
    ]

    bindgen_macros = tools.get_bindgen_defines(env.get("CPPDEFINES"), ALLPATHS)
    build_dir = str(Dir('.').abspath)

    return f'export BINDGEN_MACROS={shlex.quote(bindgen_macros)}; export BUILD_DIR=\'{build_dir}\'; cd embed/rust; cargo build --profile {RUST_PROFILE} ' + ' '.join(cargo_opts)

rust = env.Command(
    target=RUST_LIBPATH,
    source='',
    action=cargo_build(), )

env.Append(LINKFLAGS=f'-L{RUST_LIBDIR}')
env.Append(LINKFLAGS=f'-l{RUST_LIB}')
env.Append(LINKFLAGS='-lm')
env.Append(LINKFLAGS='-Wl,' + ('-dead_strip' if env['PLATFORM'] == 'darwin' else '--gc-sections'))

#
# Program objects
#

obj_program = []
obj_program += env.Object(source=SOURCE_MOD)
obj_program += env.Object(source=SOURCE_MOD_CRYPTO, CCFLAGS='$CCFLAGS -ftrivial-auto-var-init=zero')
obj_program += env.Object(source=SOURCE_BOOTLOADER)
obj_program += env.Object(source=SOURCE_NANOPB)
obj_program += env.Object(source=SOURCE_TREZORHAL)
obj_program += env.Object(source=SOURCE_UNIX)

program_elf = env.Command(
    target='bootloader.elf',
    source=obj_program,
    action=
    '$CC -o $TARGET $SOURCES $_LIBDIRFLAGS $_LIBFLAGS $LINKFLAGS', )

env.Depends(program_elf, rust)

if CMAKELISTS != 0:
    env.Depends(program_elf, cmake_gen)
