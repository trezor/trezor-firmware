# pylint: disable=E0602

import os
import tools, models

TREZOR_MODEL = ARGUMENTS.get('TREZOR_MODEL', 'T2T1')
CMAKELISTS = int(ARGUMENTS.get('CMAKELISTS', 0))
PRODUCTION = ARGUMENTS.get('PRODUCTION', '0') == '1'
HW_REVISION = ARGUMENTS.get('HW_REVISION', None)
DBG_CONSOLE = ARGUMENTS.get('DBG_CONSOLE', '')

FEATURES_WANTED = [
    "boot_ucb",
    "display",
    "kernel_mode",
    "pmic",
    "sd_card",
    "secure_domain",
    "secure_mode",
    ]

CCFLAGS_MOD = ''
CPPPATH_MOD = []
CPPDEFINES_MOD = ["BOARDLOADER"]
SOURCE_MOD = []
SOURCE_MOD_CRYPTO = []
CPPDEFINES_HAL = []
SOURCE_HAL = []
PATH_HAL = []

if DBG_CONSOLE != "":
    FEATURES_WANTED += ["dbg_console"]

env = Environment(ENV=os.environ,
    CFLAGS='%s -DPRODUCTION=%s' % (ARGUMENTS.get('CFLAGS', ''), ARGUMENTS.get('PRODUCTION', '0')),
    CPPDEFINES_IMPLICIT=[],
    CPPDEFPREFIX="-D'",
    CPPDEFSUFFIX="'",
    DBG_CONSOLE=DBG_CONSOLE,
)

FEATURES_AVAILABLE = models.configure_board(TREZOR_MODEL, HW_REVISION, FEATURES_WANTED, env, CPPDEFINES_HAL, SOURCE_HAL, PATH_HAL)

# modtrezorcrypto
CCFLAGS_MOD += '-Wno-sequence-point '
CPPPATH_MOD += [
    'vendor/trezor-crypto',
]
CPPDEFINES_MOD += [
    'AES_128',
    'AES_192',
    'USE_KECCAK',
    'ED25519_NO_PRECOMP',
]

SOURCE_MOD_CRYPTO += [
    'vendor/trezor-crypto/blake2s.c',
    'vendor/trezor-crypto/ed25519-donna/curve25519-donna-32bit.c',
    'vendor/trezor-crypto/ed25519-donna/curve25519-donna-helpers.c',
    'vendor/trezor-crypto/ed25519-donna/ed25519.c',
    'vendor/trezor-crypto/ed25519-donna/ed25519-donna-32bit-tables.c',
    'vendor/trezor-crypto/ed25519-donna/ed25519-donna-impl-base.c',
    'vendor/trezor-crypto/ed25519-donna/modm-donna-32bit.c',
    'vendor/trezor-crypto/memzero.c',
    'vendor/trezor-crypto/sha2.c',
]

if 'boot_ucb' in FEATURES_AVAILABLE:
    PQ_SIGNATURE_PATH = 'vendor/sphincsplus/ref/'
    CCFLAGS_MOD += '-DPARAMS=sphincs-sha2-128s '
    PQ_SIGNATURE_SOURCE = [
        'vendor/sphincsplus/ref/address.c',
        'vendor/sphincsplus/ref/fors.c',
        'vendor/sphincsplus/ref/hash_sha2.c',
        'vendor/sphincsplus/ref/sha2.c',
        'vendor/sphincsplus/ref/sign.c',
        'vendor/sphincsplus/ref/thash_sha2_simple.c',
        'vendor/sphincsplus/ref/utils.c',
        'vendor/sphincsplus/ref/wots.c',
    ]

# modtrezorui
CPPPATH_MOD += [
        'vendor/micropython/lib/uzlib',
]
SOURCE_MOD += [
    'embed/gfx/bitblt/gfx_bitblt.c',
    'embed/gfx/bitblt/gfx_bitblt_rgb565.c',
    'embed/gfx/bitblt/gfx_bitblt_rgba8888.c',
    'embed/gfx/bitblt/gfx_bitblt_mono8.c',
    'embed/gfx/fonts/font_bitmap.c',
    'embed/gfx/gfx_color.c',
    'embed/gfx/gfx_draw.c',
    'embed/gfx/terminal.c',
    'embed/io/display/display_utils.c',
    'embed/util/image/image.c',
    'embed/util/rsod/rsod.c',
    'embed/util/rsod/rsod_special.c',
    'embed/rtl/error_handling.c',
    'embed/rtl/scm_revision.c',
    'embed/rtl/strutils.c',
    'embed/sys/flash/flash_utils.c',
]

if not PRODUCTION:
    SOURCE_MOD += ['embed/rtl/printf.c']

SOURCE_BOARDLOADER = [
    'embed/projects/boardloader/main.c',
    'embed/projects/boardloader/bld_version.c',
]

if 'sd_card' in FEATURES_AVAILABLE:
    SOURCE_MOD += [
    'embed/projects/boardloader/sd_update.c',
    ]

env.Replace(
    CAT='cat',
    CP='cp',
    AS='arm-none-eabi-as',
    AR='arm-none-eabi-ar',
    CC='arm-none-eabi-gcc',
    LINK='arm-none-eabi-gcc',
    SIZE='arm-none-eabi-size',
    STRIP='arm-none-eabi-strip',
    OBJCOPY='arm-none-eabi-objcopy',
    PYTHON='python',
    MAKECMAKELISTS='$PYTHON tools/make_cmakelists.py',)

env.Replace(
    TREZOR_MODEL=TREZOR_MODEL, )

env.Replace(
    COPT=env.get('ENV').get('OPTIMIZE', '-Os'),
    CCFLAGS='$COPT '
    '-g3 '
    '-nostdlib '
    '-std=gnu11 -Wall -Werror -Wdouble-promotion -Wpointer-arith -Wno-missing-braces -fno-common '
    '-fsingle-precision-constant -fdata-sections -ffunction-sections '
    '-ffreestanding '
    '-fstack-protector-strong '
    + env.get('ENV')["CPU_CCFLAGS"] + CCFLAGS_MOD,
    CCFLAGS_QSTR='-DNO_QSTR -DN_X64 -DN_X86 -DN_THUMB',
    LINKFLAGS="-T build/boardloader/memory.ld -Wl,--gc-sections -Wl,-Map=build/boardloader/boardloader.map -Wl,--warn-common -Wl,--undefined=__errno -Wl,--print-memory-usage",
    CPPPATH=[
        'embed/projects/boardloader',
        'embed/rtl/inc',
        'embed/models',
        'embed/upymod/modtrezorui',
        'embed/gfx/inc',
        'embed/io/button/inc',
        'embed/io/haptic/inc',
        'embed/io/touch/inc',
        'embed/io/usb/inc',
        'embed/sys/bsp/inc',
        'embed/util/image/inc',
        'embed/util/rsod/inc',
        'vendor/micropython/lib/cmsis/inc',
    ] + CPPPATH_MOD + PATH_HAL,
    CPPDEFINES=[
        'BOARDLOADER',
        'TREZOR_MODEL_'+TREZOR_MODEL,
        'USE_HAL_DRIVER',
    ] + CPPDEFINES_MOD + CPPDEFINES_HAL,
    ASFLAGS=env.get('ENV')['CPU_ASFLAGS'],
    ASPPFLAGS='$CFLAGS $CCFLAGS',
    )

env.Replace(
    ALLSOURCES=SOURCE_MOD + SOURCE_MOD_CRYPTO + SOURCE_BOARDLOADER + SOURCE_HAL,
    ALLDEFS=tools.get_defs_for_cmake(env['CPPDEFINES'] + env['CPPDEFINES_IMPLICIT']))

cmake_gen = env.Command(
    target='CMakeLists.txt',
    source='',
    action='$MAKECMAKELISTS --sources $ALLSOURCES --dirs $CPPPATH --defs $ALLDEFS',
)

#
# Program objects
#

obj_program = []
obj_program += env.Object(source=SOURCE_MOD)
obj_program += env.Object(source=SOURCE_MOD_CRYPTO, CCFLAGS='$CCFLAGS -ftrivial-auto-var-init=zero')
obj_program += env.Object(source=SOURCE_BOARDLOADER)
obj_program += env.Object(source=SOURCE_HAL)

if 'boot_ucb' in FEATURES_AVAILABLE:
    obj_program += env.Object(
        source=PQ_SIGNATURE_SOURCE, CCFLAGS="$CCFLAGS -Wno-incompatible-pointer-types"
    )

linkerscript_gen = env.Command(
    target='memory.ld',
    source=[f'embed/models/{TREZOR_MODEL}/memory.ld', env.get('ENV')['LINKER_SCRIPT'].format(target='boardloader')],
    action='$CAT $SOURCES > $TARGET',
)

program_elf = env.Command(
    target='boardloader.elf',
    source=obj_program,
    action=
    '$LINK -o $TARGET $CCFLAGS $CFLAGS $LINKFLAGS $SOURCES -lc_nano -lm -lgcc',
)

env.Depends(program_elf, linkerscript_gen)

BINARY_NAME = f"build/boardloader/boardloader-{TREZOR_MODEL}"
BINARY_NAME += "-" + tools.get_version('embed/projects/boardloader/version.h')
BINARY_NAME += "-" + tools.get_git_revision_short_hash()
BINARY_NAME += "-dirty" if tools.get_git_modified() else ""
BINARY_NAME += ".bin"

if CMAKELISTS != 0:
    env.Depends(program_elf, cmake_gen)

program_bin = env.Command(
    target='boardloader.bin',
    source=program_elf,
    action=[
        '$OBJCOPY -O binary -j .vector_table -j .text -j .data -j .rodata -j .capabilities $SOURCE $TARGET',
        '$CP $TARGET ' + BINARY_NAME,
    ],
)
