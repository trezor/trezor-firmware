# pylint: disable=E0602
# fmt: off

import os
import tools, models, ui

BITCOIN_ONLY = ARGUMENTS.get('BITCOIN_ONLY', '0')
EVERYTHING = BITCOIN_ONLY != '1'
PRODUCTION = ARGUMENTS.get('PRODUCTION', '0') == '1'
TREZOR_MODEL = ARGUMENTS.get('TREZOR_MODEL', 'T2T1')
CMAKELISTS = int(ARGUMENTS.get('CMAKELISTS', 0))
HW_REVISION ='emulator'
THP = ARGUMENTS.get('THP', '0') == '1' # Trezor-Host Protocol
DISABLE_TROPIC = ARGUMENTS.get('DISABLE_TROPIC', '1') == '1'
BENCHMARK = ARGUMENTS.get('BENCHMARK', '0') == '1'
PYOPT = ARGUMENTS.get('PYOPT', '1')
FROZEN = ARGUMENTS.get('TREZOR_EMULATOR_FROZEN', 0)
RASPI = os.getenv('TREZOR_EMULATOR_RASPI') == '1'
MICROPY_ENABLE_SOURCE_LINE = ARGUMENTS.get('MICROPY_ENABLE_SOURCE_LINE', '1')
DBG_CONSOLE = ARGUMENTS.get('DBG_CONSOLE', '')

if BENCHMARK and PYOPT != '0':
  print("BENCHMARK=1 works only with PYOPT=0.")
  exit(1)

FEATURES_WANTED = [
    "ble",
    "display",
    "dma2d",
    "input",
    "kernel_mode",
    "optiga",
    "powerctl",
    "rgb_led",
    "sd_card",
    "secure_mode",
    "serial_number",
    "storage",
    "usb",
    "usb_iface_wire",
    "usb_iface_debug",
    ]

if PYOPT == '0':
    DBG_CONSOLE = DBG_CONSOLE or "VCP"

if DBG_CONSOLE != "":
    FEATURES_WANTED += ["dbg_console"]

if BITCOIN_ONLY == '0':
    FEATURES_WANTED += [
        "usb_iface_webauthn",
    ]

if not DISABLE_TROPIC:
    FEATURES_WANTED.append('tropic')

if not models.has_emulator(TREZOR_MODEL):
    # skip unix build
    env = Environment()
    def build_unix(target,source,env):
        print(f'Emulator: nothing to build for Model {TREZOR_MODEL}')
    program_bin = env.Command(
                 target='trezor-emu-core',
                 source=None,
                 action=build_unix)
    Return()

FEATURE_FLAGS = {
    "SECP256K1_ZKP": True,  # required for trezor.crypto.curve.bip340 (BIP340/Taproot)
    "AES_GCM": True,
}

CCFLAGS_MOD = ''
CPPPATH_MOD = []
CPPDEFINES_HAL = []
PATH_HAL = []
CPPDEFINES_MOD = []
SOURCE_MOD = [
    'vendor/micropython/extmod/vfs_posix_file.c',
]
SOURCE_MOD_CRYPTO = []
RUST_UI_FEATURES = []

# modtrezorconfig
CPPPATH_MOD += [
    'embed/upymod/modtrezorconfig',
]
SOURCE_MOD += [
    'embed/upymod/modtrezorconfig/modtrezorconfig.c',
]

# modtrezorcrypto
CCFLAGS_MOD += '-Wno-sequence-point '
CPPPATH_MOD += [
    'vendor/trezor-crypto',
]
CPPDEFINES_MOD += [
    'AES_128',
    'AES_192',
    'USE_INSECURE_PRNG',
    ('USE_BIP32_CACHE', '0'),
    ('USE_KECCAK', '1'),
    ('USE_ETHEREUM', '1' if EVERYTHING else '0'),
    ('USE_MONERO', '1' if EVERYTHING else '0'),
    ('USE_CARDANO', '1' if EVERYTHING else '0'),
    ('USE_NEM', '1' if (EVERYTHING and TREZOR_MODEL == "T2T1") else '0'),
    ('USE_EOS', '1' if (EVERYTHING and TREZOR_MODEL == "T2T1") else '0'),
    ('MICROPY_ENABLE_SOURCE_LINE', MICROPY_ENABLE_SOURCE_LINE),
]
SOURCE_MOD += [
    'embed/upymod/trezorobj.c',
    'embed/upymod/modtrezorcrypto/crc.c',
    'embed/upymod/modtrezorcrypto/modtrezorcrypto.c',
]
SOURCE_MOD_CRYPTO += [
    'vendor/trezor-crypto/address.c',
    'vendor/trezor-crypto/aes/aes_modes.c',
    'vendor/trezor-crypto/aes/aescrypt.c',
    'vendor/trezor-crypto/aes/aeskey.c',
    'vendor/trezor-crypto/aes/aestab.c',
    'vendor/trezor-crypto/base32.c',
    'vendor/trezor-crypto/base58.c',
    'vendor/trezor-crypto/bignum.c',
    'vendor/trezor-crypto/bip32.c',
    'vendor/trezor-crypto/bip39.c',
    'vendor/trezor-crypto/bip39_english.c',
    'vendor/trezor-crypto/blake256.c',
    'vendor/trezor-crypto/blake2b.c',
    'vendor/trezor-crypto/blake2s.c',
    'vendor/trezor-crypto/buffer.c',
    'vendor/trezor-crypto/chacha20poly1305/chacha20poly1305.c',
    'vendor/trezor-crypto/chacha20poly1305/chacha_merged.c',
    'vendor/trezor-crypto/chacha20poly1305/poly1305-donna.c',
    'vendor/trezor-crypto/chacha20poly1305/rfc7539.c',
    'vendor/trezor-crypto/chacha_drbg.c',
    'vendor/trezor-crypto/curves.c',
    'vendor/trezor-crypto/der.c',
    'vendor/trezor-crypto/ecdsa.c',
    'vendor/trezor-crypto/ed25519-donna/curve25519-donna-32bit.c',
    'vendor/trezor-crypto/ed25519-donna/curve25519-donna-helpers.c',
    'vendor/trezor-crypto/ed25519-donna/curve25519-donna-scalarmult-base.c',
    'vendor/trezor-crypto/ed25519-donna/ed25519-donna-32bit-tables.c',
    'vendor/trezor-crypto/ed25519-donna/ed25519-donna-basepoint-table.c',
    'vendor/trezor-crypto/ed25519-donna/ed25519-donna-impl-base.c',
    'vendor/trezor-crypto/ed25519-donna/ed25519-keccak.c',
    'vendor/trezor-crypto/ed25519-donna/ed25519-sha3.c',
    'vendor/trezor-crypto/ed25519-donna/ed25519.c',
    'vendor/trezor-crypto/ed25519-donna/modm-donna-32bit.c',
    'vendor/trezor-crypto/groestl.c',
    'vendor/trezor-crypto/hasher.c',
    'vendor/trezor-crypto/hmac.c',
    'vendor/trezor-crypto/hmac_drbg.c',
    'vendor/trezor-crypto/memzero.c',
    'vendor/trezor-crypto/nem.c',
    'vendor/trezor-crypto/nist256p1.c',
    'vendor/trezor-crypto/pbkdf2.c',
    'vendor/trezor-crypto/rand.c',
    'vendor/trezor-crypto/rand_insecure.c',
    'vendor/trezor-crypto/rfc6979.c',
    'vendor/trezor-crypto/ripemd160.c',
    'vendor/trezor-crypto/secp256k1.c',
    'vendor/trezor-crypto/segwit_addr.c',
    'vendor/trezor-crypto/sha2.c',
    'vendor/trezor-crypto/sha3.c',
    'vendor/trezor-crypto/shamir.c',
    'vendor/trezor-crypto/slip39.c',
    'vendor/trezor-crypto/slip39_english.c',
]
if EVERYTHING:
    SOURCE_MOD_CRYPTO += [
        'vendor/trezor-crypto/cardano.c',
        'vendor/trezor-crypto/monero/base58.c',
        'vendor/trezor-crypto/monero/serialize.c',
        'vendor/trezor-crypto/monero/xmr.c',
    ]

# libsecp256k1-zkp
if FEATURE_FLAGS["SECP256K1_ZKP"]:
    CPPPATH_MOD += [
        'vendor/secp256k1-zkp',
        'vendor/secp256k1-zkp/src',
        'vendor/secp256k1-zkp/include',
    ]
    CPPDEFINES_MOD += [
        'USE_SECP256K1_ZKP',
        'USE_SECP256K1_ZKP_ECDSA',
        ('SECP256K1_CONTEXT_SIZE', '208'),
        'USE_EXTERNAL_DEFAULT_CALLBACKS',
        ('ECMULT_GEN_PREC_BITS', '2'),
        ('ECMULT_WINDOW_SIZE', '2'),
        'ENABLE_MODULE_GENERATOR',
        'ENABLE_MODULE_RECOVERY',
        'ENABLE_MODULE_SCHNORRSIG',
        'ENABLE_MODULE_EXTRAKEYS',
        'ENABLE_MODULE_ECDH',
    ]
    SOURCE_MOD_SECP256K1_ZKP = [
        'vendor/secp256k1-zkp/src/secp256k1.c',
        'vendor/secp256k1-zkp/src/precomputed_ecmult.c',
        'vendor/secp256k1-zkp/src/precomputed_ecmult_gen.c',
    ]
    SOURCE_MOD_CRYPTO += [
        'vendor/trezor-crypto/zkp_context.c',
        'vendor/trezor-crypto/zkp_ecdsa.c',
        'vendor/trezor-crypto/zkp_bip340.c',
    ]

# AES-GCM
if FEATURE_FLAGS["AES_GCM"]:
    CPPDEFINES_MOD += [
        'USE_AES_GCM',
        'AES_VAR',
    ]
    SOURCE_MOD_CRYPTO += [
        'vendor/trezor-crypto/aes/gf128mul.c',
        'vendor/trezor-crypto/aes/aesgcm.c',
    ]

if THP:
    CPPDEFINES_MOD += ['USE_THP']
    SOURCE_MOD += [
        'vendor/trezor-crypto/elligator2.c',
    ]

# modtrezorio
SOURCE_MOD += [
    'embed/upymod/modtrezorio/modtrezorio.c',
]

# modtrezorui
CPPPATH_MOD += [
        'vendor/micropython/lib/uzlib',
]
SOURCE_MOD += [
    'embed/upymod/modtrezorui/modtrezorui.c',
    'embed/io/display/display_utils.c',
    'embed/io/gfx/bitblt/gfx_bitblt.c',
    'embed/io/gfx/bitblt/gfx_bitblt_rgb565.c',
    'embed/io/gfx/bitblt/gfx_bitblt_rgba8888.c',
    'embed/io/gfx/bitblt/gfx_bitblt_mono8.c',
    'embed/io/gfx/gfx_color.c',
    'embed/io/gfx/gfx_draw.c',
    'embed/io/gfx/rsod.c',
    'embed/io/gfx/terminal/font_bitmap.c',
    'embed/io/gfx/terminal/terminal.c',
    'embed/io/translations/translations.c',
    'embed/rtl/error_handling.c',
    'embed/rtl/strutils.c',
    'embed/rtl/scm_revision.c',
    'embed/sec/image/image.c',
    'embed/sec/rsod/rsod_special.c',
    'vendor/micropython/lib/uzlib/adler32.c',
    'vendor/micropython/lib/uzlib/crc32.c',
    'vendor/micropython/lib/uzlib/tinflate.c',
]

if not PRODUCTION:
    SOURCE_MOD += ['embed/rtl/printf.c']

CPPDEFINES_MOD += [
    'TRANSLATIONS',
    'FANCY_FATAL_ERROR',
    ]

if FROZEN:
    CPPDEFINES_MOD += ['TREZOR_EMULATOR_FROZEN']
if RASPI:
    CPPDEFINES_MOD += ['TREZOR_EMULATOR_RASPI']

# modtrezorutils
SOURCE_MOD += [
    'embed/upymod/modtrezorutils/modtrezorutils.c',
]

# rust mods
SOURCE_MOD += [
    'embed/upymod/rustmods.c',
]

# modutime
SOURCE_MOD += [
    'embed/upymod/modutime.c',
]

SOURCE_MICROPYTHON = [
    'vendor/micropython/extmod/modubinascii.c',
    'vendor/micropython/extmod/moductypes.c',
    'vendor/micropython/extmod/moduheapq.c',
    'vendor/micropython/extmod/moduos.c',
    'vendor/micropython/extmod/modutimeq.c',
    'vendor/micropython/extmod/utime_mphal.c',
    'vendor/micropython/shared/readline/readline.c',
    'vendor/micropython/shared/timeutils/timeutils.c',
    'vendor/micropython/py/argcheck.c',
    'vendor/micropython/py/asmarm.c',
    'vendor/micropython/py/asmbase.c',
    'vendor/micropython/py/asmthumb.c',
    'vendor/micropython/py/asmx64.c',
    'vendor/micropython/py/asmx86.c',
    'vendor/micropython/py/asmxtensa.c',
    'vendor/micropython/py/bc.c',
    'vendor/micropython/py/binary.c',
    'vendor/micropython/py/builtinevex.c',
    'vendor/micropython/py/builtinhelp.c',
    'vendor/micropython/py/builtinimport.c',
    'vendor/micropython/py/compile.c',
    'vendor/micropython/py/emitbc.c',
    'vendor/micropython/py/emitcommon.c',
    'vendor/micropython/py/emitglue.c',
    'vendor/micropython/py/emitinlinethumb.c',
    'vendor/micropython/py/emitinlinextensa.c',
    'vendor/micropython/py/emitnarm.c',
    'vendor/micropython/py/emitnative.c',
    'vendor/micropython/py/emitnthumb.c',
    'vendor/micropython/py/emitnx64.c',
    'vendor/micropython/py/emitnx86.c',
    'vendor/micropython/py/emitnxtensa.c',
    'vendor/micropython/py/formatfloat.c',
    'vendor/micropython/py/frozenmod.c',
    'vendor/micropython/py/gc.c',
    'vendor/micropython/py/lexer.c',
    'vendor/micropython/py/malloc.c',
    'vendor/micropython/py/map.c',
    'vendor/micropython/py/modarray.c',
    'vendor/micropython/py/modbuiltins.c',
    'vendor/micropython/py/modcmath.c',
    'vendor/micropython/py/modcollections.c',
    'vendor/micropython/py/modgc.c',
    'vendor/micropython/py/modio.c',
    'vendor/micropython/py/modmath.c',
    'vendor/micropython/py/modmicropython.c',
    'vendor/micropython/py/modstruct.c',
    'vendor/micropython/py/modsys.c',
    'vendor/micropython/py/modthread.c',
    'vendor/micropython/py/moduerrno.c',
    'vendor/micropython/py/mpprint.c',
    'vendor/micropython/py/mpstate.c',
    'vendor/micropython/py/mpz.c',
    'vendor/micropython/py/nativeglue.c',
    'vendor/micropython/py/nlr.c',
    'vendor/micropython/py/nlraarch64.c',
    'vendor/micropython/py/nlrsetjmp.c',
    'vendor/micropython/py/nlrthumb.c',
    'vendor/micropython/py/nlrx64.c',
    'vendor/micropython/py/nlrx86.c',
    'vendor/micropython/py/nlrxtensa.c',
    'vendor/micropython/py/obj.c',
    'vendor/micropython/py/objarray.c',
    'vendor/micropython/py/objattrtuple.c',
    'vendor/micropython/py/objbool.c',
    'vendor/micropython/py/objboundmeth.c',
    'vendor/micropython/py/objcell.c',
    'vendor/micropython/py/objclosure.c',
    'vendor/micropython/py/objcomplex.c',
    'vendor/micropython/py/objdeque.c',
    'vendor/micropython/py/objdict.c',
    'vendor/micropython/py/objenumerate.c',
    'vendor/micropython/py/objexcept.c',
    'vendor/micropython/py/objfilter.c',
    'vendor/micropython/py/objfloat.c',
    'vendor/micropython/py/objfun.c',
    'vendor/micropython/py/objgenerator.c',
    'vendor/micropython/py/objgetitemiter.c',
    'vendor/micropython/py/objint.c',
    'vendor/micropython/py/objint_longlong.c',
    'vendor/micropython/py/objint_mpz.c',
    'vendor/micropython/py/objlist.c',
    'vendor/micropython/py/objmap.c',
    'vendor/micropython/py/objmodule.c',
    'vendor/micropython/py/objnamedtuple.c',
    'vendor/micropython/py/objnone.c',
    'vendor/micropython/py/objobject.c',
    'vendor/micropython/py/objpolyiter.c',
    'vendor/micropython/py/objproperty.c',
    'vendor/micropython/py/objrange.c',
    'vendor/micropython/py/objreversed.c',
    'vendor/micropython/py/objset.c',
    'vendor/micropython/py/objsingleton.c',
    'vendor/micropython/py/objslice.c',
    'vendor/micropython/py/objstr.c',
    'vendor/micropython/py/objstringio.c',
    'vendor/micropython/py/objstrunicode.c',
    'vendor/micropython/py/objtuple.c',
    'vendor/micropython/py/objtype.c',
    'vendor/micropython/py/objzip.c',
    'vendor/micropython/py/opmethods.c',
    'vendor/micropython/py/parse.c',
    'vendor/micropython/py/parsenum.c',
    'vendor/micropython/py/parsenumbase.c',
    'vendor/micropython/py/persistentcode.c',
    'vendor/micropython/py/profile.c',
    'vendor/micropython/py/pystack.c',
    'vendor/micropython/py/qstr.c',
    'vendor/micropython/py/reader.c',
    'vendor/micropython/py/repl.c',
    'vendor/micropython/py/runtime.c',
    'vendor/micropython/py/runtime_utils.c',
    'vendor/micropython/py/scheduler.c',
    'vendor/micropython/py/scope.c',
    'vendor/micropython/py/sequence.c',
    'vendor/micropython/py/showbc.c',
    'vendor/micropython/py/smallint.c',
    'vendor/micropython/py/stackctrl.c',
    'vendor/micropython/py/stream.c',
    'vendor/micropython/py/unicode.c',
    'vendor/micropython/py/vm.c',
    'vendor/micropython/py/vstr.c',
    'vendor/micropython/py/warning.c',
]

SOURCE_UNIX = [
    'embed/projects/unix/main_main.c',
    'embed/projects/unix/main.c',
    'embed/projects/unix/profile.c',
    'vendor/micropython/ports/unix/alloc.c',
    'vendor/micropython/ports/unix/gccollect.c',
    'vendor/micropython/ports/unix/input.c',
    'vendor/micropython/ports/unix/unix_mphal.c',
    'vendor/micropython/shared/runtime/gchelper_generic.c',
]

TRANSLATION_DATA = [
    "translations/en.json",
    "translations/order.json",
]

ui.init_ui(TREZOR_MODEL, "firmware", RUST_UI_FEATURES)

SOURCE_QSTR = SOURCE_MOD + SOURCE_MICROPYTHON + SOURCE_UNIX

if PYOPT == '0' or not FROZEN:
    DEBUG_FLAGS = "-DMICROPY_OOM_CALLBACK=1 -DSTATIC="
else:
    DEBUG_FLAGS = "-DMICROPY_OOM_CALLBACK=0"

env = Environment(
    ENV=os.environ,
    CFLAGS=ARGUMENTS.get("CFLAGS", "") + f" -DCONFIDENTIAL= -DPYOPT={PYOPT} -DBITCOIN_ONLY={BITCOIN_ONLY} {DEBUG_FLAGS}",
    CPPDEFPREFIX="-D'",
    CPPDEFSUFFIX="'",
)

FEATURES_AVAILABLE = models.configure_board(TREZOR_MODEL, HW_REVISION, FEATURES_WANTED, env, CPPDEFINES_HAL, SOURCE_UNIX, PATH_HAL)

env.Tool('micropython')

env.Replace(
    CC=os.getenv('CC') or 'gcc',
    SED='sed',
    AS='as',
    AR='ar',
    LINK='ld',
    SIZE='size',
    STRIP='strip',
    OBJCOPY='objcopy',
    COPT=os.getenv('OPTIMIZE', '-Os'), )

if ARGUMENTS.get('TREZOR_EMULATOR_CLANG', '0') == '1':
    env.Replace(
        CC='clang',
        LINK='clang', )

if ARGUMENTS.get('TREZOR_EMULATOR_ASAN', '0') == '1':
    asan_flags=(
        ' -fsanitize=address,undefined'+
        ' -fno-omit-frame-pointer'+
        ' -fno-optimize-sibling-calls'
    )
    env.Append(
        CFLAGS=asan_flags,
        LINKFLAGS=asan_flags,
        LDFLAGS=asan_flags,
        ENV={
            # workaround for sanitizers being nightly-only
            # remove after stabilized https://github.com/rust-lang/rust/issues/39699
            'RUSTC_BOOTSTRAP': '1',
            'RUSTFLAGS': '-Z sanitizer=address',
        }, )

if ARGUMENTS.get('TREZOR_EMULATOR_DEBUGGABLE', '0') == '1':
    env.Replace(
        COPT=' -Og -ggdb',
        STRIP='true', )

if ARGUMENTS.get('TREZOR_MEMPERF', '0') == '1':
    CPPDEFINES_MOD += [
        ('MICROPY_TREZOR_MEMPERF', '1')
    ]

env.Replace(
    TREZOR_MODEL=TREZOR_MODEL, )

ALLPATHS=['.',
         'embed/rust',
         'embed/rtl/inc',
         'embed/models',
         'embed/projects/unix',
         'embed/upymod/modtrezorui',
         'embed/io/gfx/inc',
         'embed/io/translations/inc',
         'embed/sec/image/inc',
         'embed/sec/rsod/inc',
         'vendor/micropython',
         'vendor/micropython/ports/unix',
         'vendor/micropython/lib/mp-readline',
     ] + CPPPATH_MOD + PATH_HAL

env.Replace(
    CCFLAGS='$COPT '
    '-g '
    '-std=gnu11 -Wall -Werror -Wuninitialized -Wno-missing-braces '
    '-fdata-sections -ffunction-sections -fPIE ' + CCFLAGS_MOD,
    CCFLAGS_QSTR='-DNO_QSTR -DN_X64 -DN_X86 -DN_THUMB',
    LIBS=['m'],
    CPPPATH=ALLPATHS,
    CPPDEFINES=[
        'TREZOR_EMULATOR',
        'TREZOR_MODEL_'+TREZOR_MODEL,
        ('MP_CONFIGFILE', '"embed/projects/unix/mpconfigport.h"'),
        ui.get_ui_layout(TREZOR_MODEL),
    ] + CPPDEFINES_MOD + CPPDEFINES_HAL,
    ASPPFLAGS='$CFLAGS $CCFLAGS', )

try:
    env.ParseConfig('pkg-config --cflags --libs libjpeg')
except OSError:
    print("libjpeg not installed, Emulator build is not possible")

try:
    env.ParseConfig('pkg-config --cflags --libs sdl2 SDL2_image')
except OSError:
    print("SDL2 not installed, Emulator build is not possible")

BYTECODE_OPTIMIZATION = {'0': '0', '1': '3'}[PYOPT]

env.Replace(
    PYTHON='python',
    MAKECOMPRESSEDDATA='$PYTHON vendor/micropython/py/makecompresseddata.py',
    MAKEQSTRDATA='$PYTHON vendor/micropython/py/makeqstrdata.py',
    MAKEVERSIONHDR='$PYTHON vendor/micropython/py/makeversionhdr.py',
    MAKEMODULEDEFS='$PYTHON vendor/micropython/py/makemoduledefs.py',
    MAKECMAKELISTS='$PYTHON tools/make_cmakelists.py',
    MPY_TOOL='$PYTHON vendor/micropython/tools/mpy-tool.py',
    MPY_CROSS='vendor/micropython/mpy-cross/mpy-cross -O' + BYTECODE_OPTIMIZATION,
    PB2PY='$PYTHON ../common/protob/pb2py',
)

#
# Qstrings
#

PROTO_SOURCES_DIR = '../../../common/protob/'

exclude_list = [PROTO_SOURCES_DIR + 'messages-bootloader.proto']
if not THP:
    exclude_list.append(PROTO_SOURCES_DIR + 'messages-thp.proto')
if PYOPT != '0':
    exclude_list.append(PROTO_SOURCES_DIR + 'messages-debug.proto')

PROTO_SOURCES = Glob(PROTO_SOURCES_DIR + '*.proto',
    exclude=exclude_list
)

qstr_protobuf = env.Command(
    target=[
        'genhdr/qstrdefs.protobuf.h',
    ],
    source=PROTO_SOURCES,
    action='$PB2PY $SOURCES --qstr-out ${TARGET} --bitcoin-only=%s' % BITCOIN_ONLY,
)

qstr_micropython = 'vendor/micropython/py/qstrdefs.h'

qstr_extra = 'embed/upymod/qstrdefsport.h'

micropy_defines = env.MicroPyDefines(source=SOURCE_QSTR)

qstr_collected = env.CollectQstr(
    target='genhdr/qstrdefs.collected.h', source=micropy_defines)

qstr_preprocessed = env.PreprocessQstr(
    target='genhdr/qstrdefs.preprocessed.h',
    source=[qstr_micropython, qstr_protobuf, qstr_collected, qstr_extra])

qstr_generated = env.GenerateQstrDefs(
    target='genhdr/qstrdefs.generated.h', source=qstr_preprocessed)

env.Ignore(qstr_collected, qstr_generated)

#
# Micropython module declarations
#

moduledefs_collected = env.CollectModules(
    target='genhdr/moduledefs.collected.h', source=micropy_defines)


hdr_moduledefs = env.Command(
    target='genhdr/moduledefs.h',
    source=moduledefs_collected,
    action='$MAKEMODULEDEFS $SOURCE > $TARGET', )

env.Ignore(moduledefs_collected, moduledefs_collected)
env.Ignore(moduledefs_collected, qstr_generated)
env.Ignore(moduledefs_collected, hdr_moduledefs)

#
# Compressed Qstrings
#

compressed_collected = env.CollectCompressed(
    target='genhdr/compressed.collected', source=micropy_defines)

compressed_data = env.GenerateCompressed(
    target='genhdr/compressed.data.h', source=compressed_collected)

env.Ignore(micropy_defines, compressed_data)

#
# Micropython version
#

hdr_version = env.Command(
    target='genhdr/mpversion.h',
    source='',
    action='$MAKEVERSIONHDR $TARGET', )

env.Ignore(micropy_defines, hdr_moduledefs)
env.Ignore(micropy_defines, qstr_collected)
env.Ignore(micropy_defines, qstr_preprocessed)
env.Ignore(micropy_defines, qstr_generated)

#
# Frozen modules
#

if FROZEN:
    SOURCE_PY_DIR = 'src/'

    SOURCE_PY = Glob(SOURCE_PY_DIR + '*.py',
        exclude=[
            SOURCE_PY_DIR + 'ble.py',
        ] if "ble" not in FEATURES_AVAILABLE else []
    )
    SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'trezor/*.py',
        exclude=[
            SOURCE_PY_DIR + 'trezor/sdcard.py',
        ] if 'sd_card' not in FEATURES_AVAILABLE else []
    ))
    SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'trezor/crypto/*.py'))
    SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'trezor/ui/*.py'))

    # UI layouts - common files and then model-specific. Exclude FIDO when BTC-only.
    SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'trezor/ui/layouts/*.py',
        exclude=[
            SOURCE_PY_DIR + 'trezor/ui/layouts/fido.py',
        ] if not EVERYTHING else []
    ))

    layout_path = ui.get_ui_layout_path(TREZOR_MODEL)

    SOURCE_PY.extend(Glob(SOURCE_PY_DIR + layout_path + '*.py',
        exclude=[
            SOURCE_PY_DIR + layout_path + 'fido.py',
        ] if not EVERYTHING else []
    ))

    if THP:
        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'trezor/wire/thp/*.py'))
    if not THP or PYOPT == '0':
        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'trezor/wire/codec/*.py'))

    SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'trezor/wire/*.py',
        exclude=[
            SOURCE_PY_DIR + 'trezor/wire/wire_log.py'
        ] if PYOPT != '0' else []
    ))

    if 'power_manager' in FEATURES_AVAILABLE:
        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'trezor/power_management/*.py'))

    SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'storage/*.py',
        exclude=(
            ([SOURCE_PY_DIR + 'storage/sd_salt.py'] if 'sd_card' not in FEATURES_AVAILABLE else []) +
            ([SOURCE_PY_DIR + 'storage/debug.py'] if PYOPT != '0' else []) +
            ([
                SOURCE_PY_DIR + 'storage/cache_codec.py',
                SOURCE_PY_DIR + 'storage/cache_codec_keys.py'
            ] if THP and PYOPT != '0' else []) +
            ([
                SOURCE_PY_DIR + 'storage/cache_thp.py',
                SOURCE_PY_DIR + 'storage/cache_thp_keys.py'
            ] if not THP else [])
        )
    ))

    SOURCE_PY_DEBUG_ENUMS = [SOURCE_PY_DIR + 'trezor/enums/Debug*.py']
    SOURCE_PY_THP_ENUMS = [SOURCE_PY_DIR + 'trezor/enums/Thp*.py']
    SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'trezor/enums/*.py',
        exclude=[
            SOURCE_PY_DIR + 'trezor/enums/Cardano*.py',
            SOURCE_PY_DIR + 'trezor/enums/DebugMonero*.py',
            SOURCE_PY_DIR + 'trezor/enums/DefinitionType.py',
            SOURCE_PY_DIR + 'trezor/enums/Eos*.py',
            SOURCE_PY_DIR + 'trezor/enums/Ethereum*.py',
            SOURCE_PY_DIR + 'trezor/enums/Monero*.py',
            SOURCE_PY_DIR + 'trezor/enums/NEM*.py',
            SOURCE_PY_DIR + 'trezor/enums/Ripple*.py',
            SOURCE_PY_DIR + 'trezor/enums/Solana*.py',
            SOURCE_PY_DIR + 'trezor/enums/Stellar*.py',
            SOURCE_PY_DIR + 'trezor/enums/Tezos*.py',
            SOURCE_PY_DIR + 'trezor/enums/Zcash*.py',
            SOURCE_PY_DIR + 'trezor/enums/Tron*.py',
        ] + (
            SOURCE_PY_DEBUG_ENUMS if PYOPT != '0' else []
        ) + (
            SOURCE_PY_THP_ENUMS if not THP else []
        )
    ))

    SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/*.py'))
    SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/common/*.py',
        exclude=[
            SOURCE_PY_DIR + 'apps/common/definitions.py',
            SOURCE_PY_DIR + 'apps/common/definitions_constants.py',
        ] + (
            [SOURCE_PY_DIR + 'apps/common/sdcard.py'] if "sd_card" not in FEATURES_AVAILABLE else []
        )
    ))
    if PYOPT == '0':
        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/debug/*.py'))
    SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/homescreen/*.py',
        exclude=[
            SOURCE_PY_DIR + 'apps/homescreen/device_menu.py',
        ] if TREZOR_MODEL != "T3W1" else [])
    )
    SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/management/*.py',
        exclude=(
            [SOURCE_PY_DIR + 'apps/management/sd_protect.py'] if "sd_card" not in FEATURES_AVAILABLE else []
        ) + (
            [SOURCE_PY_DIR + 'apps/management/authenticate_device.py'] if "optiga" not in FEATURES_AVAILABLE else []
        ) + (
            [SOURCE_PY_DIR + 'apps/management/get_serial_number.py'] if "serial_number" not in FEATURES_AVAILABLE else []
        )
    )
    )
    SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/management/*/*.py',
        exclude=(
            [SOURCE_PY_DIR + 'apps/management/ble/*.py'] if "ble" not in FEATURES_AVAILABLE else []
        ))
    )
    SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/misc/*.py'))
    SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/bitcoin/*.py'))
    SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/bitcoin/*/*.py',
        exclude=[
            SOURCE_PY_DIR + 'apps/bitcoin/sign_tx/decred.py',
            SOURCE_PY_DIR + 'apps/bitcoin/sign_tx/bitcoinlike.py',
            SOURCE_PY_DIR + 'apps/bitcoin/sign_tx/zcash_v4.py',
        ])
    )

    SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/evolu/*.py'))

    if BENCHMARK:
        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/benchmark/*.py'))

    if THP:
        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/thp/*.py'))

    if EVERYTHING:
        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/common/definitions.py'))
        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/common/definitions_constants.py'))
        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'trezor/enums/DefinitionType.py'))

        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/cardano/*.py'))
        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/cardano/*/*.py'))
        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'trezor/enums/Cardano*.py'))

        if TREZOR_MODEL == "T2T1":
            SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/eos/*.py'))
            SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/eos/*/*.py'))
            SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'trezor/enums/Eos*.py'))

        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/ethereum/*.py'))
        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'trezor/enums/Ethereum*.py'))

        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/monero/*.py'))
        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/monero/*/*.py'))
        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/monero/*/*/*.py'))
        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'trezor/enums/DebugMonero*.py'))
        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'trezor/enums/Monero*.py'))

        if TREZOR_MODEL == "T2T1":
            SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/nem/*.py'))
            SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/nem/*/*.py'))
            SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'trezor/enums/NEM*.py'))

        if PYOPT == '0':
            SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/nostr/*.py'))
            SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/nostr/*/*.py'))
            SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'trezor/enums/Nostr*.py'))
            SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/tron/*.py'))
            SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'trezor/enums/Tron*.py'))

        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/ripple/*.py'))
        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'trezor/enums/Ripple*.py'))

        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/solana/*.py'))
        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/solana/*/*.py'))
        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'trezor/enums/Solana*.py'))

        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/stellar/*.py'))
        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/stellar/*/*.py'))
        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'trezor/enums/Stellar*.py'))

        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/tezos/*.py'))
        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'trezor/enums/Tezos*.py'))

        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/zcash/*.py'))

        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/webauthn/*.py'))

        if TREZOR_MODEL == "T2T1":
            SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/bitcoin/sign_tx/decred.py'))
        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/bitcoin/sign_tx/bitcoinlike.py'))
        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/bitcoin/sign_tx/zcash_v4.py'))
        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'trezor/enums/Zcash*.py'))

    if PYOPT == '0':
        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'prof/*.py'))


    source_mpy = env.FrozenModule(
        source=SOURCE_PY,
        source_dir=SOURCE_PY_DIR,
        bitcoin_only=BITCOIN_ONLY,
        backlight='backlight' in FEATURES_AVAILABLE,
        optiga='optiga' in FEATURES_AVAILABLE,
        tropic='tropic' in FEATURES_AVAILABLE,
        use_ble='ble' in FEATURES_AVAILABLE,
        use_button='button' in FEATURES_AVAILABLE,
        use_touch='touch' in FEATURES_AVAILABLE,
        ui_layout=ui.get_ui_layout(TREZOR_MODEL),
        thp=THP,
        power_manager='power_manager' in FEATURES_AVAILABLE,
        )

    source_mpyc = env.FrozenCFile(
        target='frozen_mpy.c', source=source_mpy, qstr_header=qstr_preprocessed)

    env.Depends(source_mpyc, qstr_generated)


#
# Program objects
#

obj_program = []
source_files = SOURCE_MOD + SOURCE_MOD_CRYPTO + SOURCE_MICROPYTHON + SOURCE_UNIX
obj_program.extend(env.Object(source=SOURCE_MOD))
obj_program.extend(env.Object(source=SOURCE_MOD_CRYPTO, CCFLAGS='$CCFLAGS -ftrivial-auto-var-init=zero'))
if FEATURE_FLAGS["SECP256K1_ZKP"]:
    obj_program.extend(env.Object(source=SOURCE_MOD_SECP256K1_ZKP, CCFLAGS='$CCFLAGS -Wno-unused-function'))
    source_files.extend(SOURCE_MOD_SECP256K1_ZKP)
obj_program.extend(env.Object(source=SOURCE_MICROPYTHON))
obj_program.extend(env.Object(source=SOURCE_UNIX))
if FROZEN:
    obj_program.extend(env.Object(source=source_mpyc))

env.Replace(
    ALLSOURCES=source_files,
    ALLDEFS=tools.get_defs_for_cmake(env['CPPDEFINES']))


cmake_gen = env.Command(
    target='CMakeLists.txt',
    source='',
    action='$MAKECMAKELISTS --sources $ALLSOURCES --dirs $CPPPATH --defs $ALLDEFS',
)


#
# Rust library
#

protobuf_blobs = env.Command(
    target=[
        'rust/proto_enums.data',
        'rust/proto_msgs.data',
        'rust/proto_names.data',
        'rust/proto_wire.data',
    ],
    source=PROTO_SOURCES,
    action='$PB2PY --bitcoin-only=%s --blob-outdir ${TARGET.dir} $SOURCES --qstr-defs build/unix/genhdr/qstrdefs.generated.h' % BITCOIN_ONLY,
)
env.Depends(protobuf_blobs, qstr_generated)



#
# Rust library
#
env.get("ENV")["RUST_TARGET"] = os.popen("rustc -vV | sed -n 's/host: //p'").read().strip()
if PYOPT == '0' or ARGUMENTS.get('TREZOR_EMULATOR_DEBUGGABLE', '0') == '1':
    profile = 'dev'
else:
    profile = 'release'

features = ['micropython', 'protobuf', 'ui', 'translations'] + FEATURES_AVAILABLE + RUST_UI_FEATURES
if PYOPT == '0':
    features.append('debug')
    features.append('ui_debug')
    features.append('ui_debug_overlay')
if EVERYTHING:
    features.append('universal_fw')
if not PRODUCTION:
    features.append('dev_keys')

rust = tools.add_rust_lib(
    env=env,
    build='unix',
    profile=profile,
    features=features,
    all_paths=ALLPATHS,
    build_dir=str(Dir('.').abspath),
)


env.Depends(rust, protobuf_blobs)
env.Depends(rust, TRANSLATION_DATA)
env.Depends(obj_program, qstr_generated)

program = env.Command(
    target='trezor-emu-core',
    source=obj_program,
    action='$CC -o $TARGET $SOURCES $_LIBDIRFLAGS $_LIBFLAGS $LINKFLAGS', )

if CMAKELISTS != 0:
    env.Depends(program, cmake_gen)
env.Depends(program, rust)
